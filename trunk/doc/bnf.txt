<!-- Start Symbol -->
<START_SYM> ::= <PROGRAMA> $

<PROGRAMA>  ::= <DEF_TIPOS> <DEF_VAR> <EJECUCION> |
                <DEF_VAR> <EJECUCION> |
                <EJECUCION>

<DEF_TIPOS> ::= <DEF_TIPO> | 
                <DEF_TIPOS> <DEF_TIPO>
<DEF_TIPO>  ::= <TYPE> <ID> <AS> <LISTA> <PUNTO_Y_COMA>

<LISTA>     ::= <LISTA_NUM> | 
                <LISTA_STR>

<LISTA_NUM> ::= <COR_ABRE> <LIS_NUM_C> <COR_CIERRA>
<LIS_NUM_C> ::= <CTE_NUM> | 
                <LIS_NUM_C> <COMA> <CTE_NUM>

<LISTA_STR> ::= <COR_ABRE> <LIS_STR_C> <COR_CIERRA>
<LIS_STR_C> ::= <CTE_STR> | 
                <LIS_STR_C> <COMA> <CTE_STR>

<DEF_VAR>   ::= <DEFVAR> <LISTA_VAR> <ENDDEF>
<LISTA_VAR> ::= <LISTA_IDS> <DOS_PUNTOS> <TIPO> <PUNTO_Y_COMA> |
                <LISTA_VAR> <LISTA_IDS> <DOS_PUNTOS> <TIPO> <PUNTO_Y_COMA>
<LISTA_IDS> ::= <ID> | 
                <LISTA_IDS> <COMA> <ID>

<TIPO>      ::= <FLOAT> | 
                <STRING> | 
                <POINTER> | 
                <ID>

<EJECUCION> ::= <BEGIN> <SENTENCIAS> <END> | 
                <BEGIN> <END>

<SENTENCIAS>::= <SENTENCIA> | 
                <SENTENCIAS> <SENTENCIA>
                
<SENTENCIA> ::= <ASIGNACION> | 
                <CONDICIONAL> | <BUCLE> | 
                <DISPLAY_COMMAND>

<ASIGNACION>::= <ID> <OP_ASIG> <EXPRESION> <PUNTO_Y_COMA>

<EXPRESION> ::= <TERMINO> |
                <EXPRESION> <OP_SUMA> <TERMINO> |
                <EXPRESION> <OP_RESTA> <TERMINO>

<TERMINO>   ::= <FACTOR> |
                <TERMINO> <OP_MUL> <FACTOR> |
                <TERMINO> <OP_DIV> <FACTOR>

<FACTOR>    ::= <ID> |
                <CTE_NUM> |
                <PAR_ABRE> <EXPRESION> <PAR_CIERRA> |
                <AVERAGE>

<CONDICIONAL>::= <IF> <PAR_ABRE> <CONDICION> <PAR_CIERRA> <SENTENCIAS> <ENDIF> |
                 <IF> <PAR_ABRE> <CONDICION> <PAR_CIERRA> <SENTENCIAS> <ELSE> <SENTENCIAS> <ENDIF>

<CONDICION> ::= <COMPARACION> |
                <OP_NEGACION> <PAR_ABRE> <COMPARACION> <PAR_CIERRA> |
                <COMPARACION> <AND> <COMPARACION> |
                <COMPARACION> <OR> <COMPARACION>

<COMPARACION> ::= <EXPRESION> <OP_IGUAL> <EXPRESION> |
                  <EXPRESION> <OP_DISTINTO> <EXPRESION> |
                  <EXPRESION> <OP_MAYOR> <EXPRESION> |
                  <EXPRESION> <OP_MENOR> <EXPRESION> |
                  <EXPRESION> <OP_MAYOR_IGUAL> <EXPRESION> |
                  <EXPRESION> <OP_MENOR_IGUAL> <EXPRESION> |

<BUCLE>     ::= <REPEAT> <SENTENCIAS> <UNTIL> <PAR_ABRE> <CONDICION> <PAR_CIERRA> <PUNTO_Y_COMA>

<DISPLAY_COMMAND>   ::= <DISPLAY> <PAR_ABRE> <CTE_STR> <PAR_CIERRA> <PUNTO_Y_COMA>

<AVERAGE>       ::= <AVG> <PAR_ABRE> <LISTA_NUM> <PAR_CIERRA>

<!-- Símbolos Terminales del Analizador Sintáctico -->

<ID>            ::= <LETRA> |
                    <ID> <LETRA> |
                    <ID> <DIGITO>
<CTE_NUM>       ::= <ENTERO> |
                    <DECIMAL> |
                    <ENTERO> <DECIMAL>
<CTE_STR>       ::= "<TEXTO>" |
                    ""
<OP_SUMA>       ::= +
<OP_RESTA>      ::= -
<OP_MUL>        ::= *
<OP_DIV>        ::= /
<OP_ASIG>       ::= =
<AND>           ::= &&
<OR>            ::= ||
<OP_NEGACION>   ::= !
<OP_IGUAL>      ::= ==
<OP_DISTINTO>   ::= !=
<OP_MAYOR>      ::= >
<OP_MENOR>      ::= <
<OP_MAYOR_IGUAL>::= >=
<OP_MENOR_IGUAL>::= <=
<PAR_ABRE>      ::= (
<PAR_CIERRA>    ::= )
<COR_ABRE>      ::= [
<COR_CIERRA>    ::= ]
<COMA>          ::= ,
<DOS_PUNTOS>    ::= :
<PUNTO_Y_COMA>  ::= ;
<BEGIN>         ::= BEGIN
<END>           ::= END
<STRING>        ::= STRING
<FLOAT>         ::= FLOAT
<POINTER>       ::= POINTER
<DEFVAR>        ::= DEFVAR
<ENDDEF>        ::= ENDDEF
<IF>            ::= IF
<ELSE>          ::= ELSE
<ENDIF>         ::= ENDIF
<REPEAT>        ::= REPEAT
<UNTIL>         ::= UNTIL
<TYPE>          ::= TYPE
<AS>            ::= AS
<DISPLAY>       ::= DISPLAY
<AVG>           ::= AVG